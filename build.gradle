buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '8.0.0+'
    id 'java'
}

group = 'com.sakurarealm'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    flatDir {
        dirs 'libs'
    }
}

configurations {
    shadow
    implementation.extendsFrom shadow
}

dependencies {
    compileOnly name: "spigot-1.12-R0.1-SNAPSHOT"

    implementation "mysql:mysql-connector-java:8.0.33"

    shadow 'com.baomidou:mybatis-plus:3.4.3'
    shadow 'com.baomidou:mybatis-plus-extension:3.4.3'
    shadow "org.yaml:snakeyaml:2.0"
}

shadowJar {
    configurations = [project.configurations.shadow]
    archivesBaseName = project.name
    archiveClassifier = 'fat'

    // Relocate shadowed resources
    relocate "org.yaml", "${project.group}.shadow.org.yaml"
    relocate "com.baomidou", "${project.group}.shadow.com.baomidou"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version, name: project.name]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.jar.dependsOn(processResources)
tasks.shadowJar.dependsOn(processResources)